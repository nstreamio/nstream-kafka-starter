buildscript {
  repositories {
    maven { url 'https://plugins.gradle.org/m2/' }
  }
}

apply plugin: 'application'

repositories {
  mavenCentral()
  maven { url "https://packages.confluent.io/maven" }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  implementation group: 'org.swimos', name: 'swim-api', version: "$SWIMOS_VERSION"
  implementation group: 'org.swimos', name: 'swim-server', version: "$SWIMOS_VERSION"
  implementation group: 'org.swimos', name: 'swim-client', version: "$SWIMOS_VERSION"

  implementation group: 'io.nstream', name: 'nstream-adapter-runtime', version: "$NSTREAM_VERSION"
  implementation group: 'io.nstream', name: 'nstream-adapter-common', version: "$NSTREAM_VERSION"
  implementation group: 'io.nstream', name: 'nstream-adapter-kafka', version: "$NSTREAM_VERSION"

  // Use TestNG framework, also requires calling test.useTestNG() below
  testImplementation 'org.testng:testng:7.4.0'
}

application {
  // Define the main class for the application.
  mainClass = 'nstream.adapter.runtime.AppPlane'
}

task runSim(type: JavaExec) {
  group = 'application'
  classpath sourceSets.main.runtimeClasspath
  mainClass = 'nstream.starter.sim.SimLauncher'
}

compileJava {
  // Hacky workaround to Gradle's inability to infer automatic unnamed modules
  modularity.inferModulePath = false
  doFirst {
    options.compilerArgs += ['--module-path', classpath.asPath]
  }
}

tasks.named('test') {
  // Use TestNG for unit tests.
  useTestNG()
}

task copyDockerResources(type: Copy) {
  from tasks.jar.outputs
  from configurations.runtimeClasspath
  into "${buildDir}/docker/libs"
}

task copyDockerfile(type: Copy) {
  from 'docker/Dockerfile'
  into "${buildDir}/docker"
}

task dockerBuildImage() {
  dependsOn copyDockerResources
  dependsOn copyDockerfile
}
